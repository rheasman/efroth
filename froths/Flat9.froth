Program("Simple flat 9 bar 30s 92C shot", 500, 120)

// Define word "SetPressure" to set the machine pressure
: SetPressure  
  IO_Pressure  // Put constant referring to Pressure on stack
  IOW          // Write pressure to DE1
  ;

: SetTemp       // Define word "SetTemp" to set the machine shower head and group temperature
  DUP           // Make a copy of the temperature
  IO_ShowerTemp // Put constant referring to shower head temperature on stack
  IOW           // Set target temp
  IO_GroupMetalTemp  // Group head metal temperature
  IOW           // Set target temp
  ;

: GetSeconds     // Get number of seconds elapsed since start of shot
  IO_NumSeconds  // Put constant referring to number of seconds since start of shot on stack
  IOR            // Read number of seconds
  ;

// Return 1 if we've reached the given number of seconds
:(SLength -- ReachedBool) SecsReached
  GetSeconds     // x secs   Get the number of seconds elapsed
  SWAP           // secs x   Swap x and seconds
  TGE            // (secs>=x) 1 if seconds > x, else 0
  ;

: Idle
  STOP
  ;
: Halt
  STOP
;

: Wait1Sec  // Random test code to test FOR loops and IF statements
  20 1 0 FOR
    6 1 0 FOR
      WAIT
      0 INDEX
      1 INDEX
      DROP
      DROP
    ENDFOR
  
    1
    IF
      10
    ELSE
      11
    ENDIF
    DROP
  ENDFOR
;  

: TestMath // Check to see how math looks in the symbolic debugger
  GetSeconds 5 <A> +
  7 <B> 9 <C> -
  *
  3.5
  /
;

// Define the actual shot
: RunShot
  TestMath
  92.5 SetTemp    // Set group temperature to 92 deg C
  9 SetPressure   // Set the pressure target
{NotDoneYet}      // Define a label
  Wait1Sec        // Wait 1s
  30 SecsReached  // Returns 1 if seconds reached, else zero
  NotDoneYet BZ   // Jump to label if seconds not reached
  STOP            // Tell DE1 to stop
;